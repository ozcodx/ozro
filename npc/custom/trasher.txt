/*=========================================================
Garrett the Scavenger
by Ozcodex
===========================================================
Description:
A shrewd merchant with an eye for hidden value.
He sees potential in items others might overlook,
selecting two different items each day to purchase
at premium prices.
===========================================================
Compatibility:
Optimised for Hercules emulators.
===========================================================
Changelog:
v1.0 - First version.
v1.1 - Added price limits to maintain game economy
v2.0 - Major refactor:
       - Changed to 2 items (1 common, 1 misc) instead of 3
       - Improved item selection to avoid duplicates
       - Added daily spending tracking and limits
       - Added historical spending tracking
       - Improved price calculation with configurable increment
       - Enhanced user interface and item display
       - Added option to check total historical spending
       - Better handling of insufficient items case
v2.1 - Character enhancement:
       - Renamed to Garrett the Scavenger
       - Added more personality to dialogues
       - Improved item inspection narrative
=========================================================*/

geffen,143,70,3	script	Garrett	1_M_SIGN1,{
	
	/*-----------------------------------------------------
	Script
	-----------------------------------------------------*/
	// Initialize account variables if they don't exist (only when player interacts)
	if (#TRSH_DailySpend <= 0) #TRSH_DailySpend = 0;
	if (#TRSH_TotalSpent <= 0) #TRSH_TotalSpent = 0;
	
	mes .npc_name$;
	
	// Check if we have reached the daily spending limit
	if (#TRSH_DailySpend >= .daily_spending_limit) {
		mes "Ah, my friend! I've exhausted my coin purse for today.";
		mes "The market's been quite generous, hasn't it?";
		mes "Return tomorrow, I'm sure I'll have fresh opportunities for us both.";
		close;
	}
	
	// Get weekday and check if we need to update
	.@weekday = gettime(GETTIME_WEEKDAY);
	
	// Update items if:
	// - It's a different day than last update
	// - LastTrashDay is invalid (not 0-6)
	// - Any selected item is invalid (id = 0)
	if (.@weekday != #LastTrashDay || 
		#LastTrashDay < 0 || #LastTrashDay > 6 ||
		@TodayItem0 == 0 || 
		@TodayItem1 == 0) {
		
		// Reset daily spending and update last day
		#LastTrashDay = .@weekday;
		#TRSH_DailySpend = 0;
		
		// Select one item from common items
		.@rand = rand(getarraysize(.common_items));
		@TodayItem0 = .common_items[.@rand];
		
		mes "dbg: random index: " + .@rand;
		mes "array size: " + getarraysize(.common_items);
		mes "the 5th commonitem id: " + .common_items[5];
		mes "a specified id: " + getitemname(920);
		mes "the price of the specified id: " + getiteminfo(920, ITEMINFO_SELLPRICE);
		mes "dbg: item id: " + @TodayItem0;
		
		// Select one item from misc items
		.@rand = rand(1,getarraysize(.@misc_items));
		@TodayItem1 = .@misc_items[.@rand];

		mes "dbg: random index: " + .@rand;
		mes "array size: " + getarraysize(.misc_items);
		mes "the 5th misc item id: " + .misc_items[5];
		mes "a specified id: " + getitemname(7150);
		mes "the price of the specified id: " + getiteminfo(7150, ITEMINFO_SELLPRICE);
		mes "dbg: item id: " + @TodayItem1;
	}
	
	// Calculate prices for both items
	.@item_id0 = @TodayItem0;
	.@base_price0 = getiteminfo(.@item_id0, ITEMINFO_SELLPRICE);
	.@price0 = .@base_price0 * .increment;
	if (.@price0 < .min_price) .@price0 = .min_price;
	if (.@price0 > .max_price) .@price0 = .max_price;
	
	.@item_id1 = @TodayItem1;
	.@base_price1 = getiteminfo(.@item_id1, ITEMINFO_SELLPRICE);
	.@price1 = .@base_price1 * .increment;
	if (.@price1 < .min_price) .@price1 = .min_price;
	if (.@price1 > .max_price) .@price1 = .max_price;
	
	mes "Ah, welcome friend! They say one person's trash is another's fortune...";
	mes "And today, I've got my eye on some particularly interesting items.";
	next;
	
	mes .npc_name$;
	mes "My purse still holds " + (.daily_spending_limit - #TRSH_DailySpend) + " zeny for today's ventures.";
	next;
	
	mes .npc_name$;
	mes "Now, let me tell you what's caught my expert eye:";
	mes "- A fine " + getitemname(@TodayItem0) + " - I'll offer " + .@price0 + " zeny each";
	mes "- And any " + getitemname(@TodayItem1) + " - worth " + .@price1 + " zeny to me";
	
	next;
	switch(select("Trade with Garrett:Ask about business:Leave")) {
		case 1:
			mes .npc_name$;
			mes "Excellent! Which treasure would you like to part with?";
			next;
			
			.@menu$ = getitemname(@TodayItem0);
			.@menu$ += ":" + getitemname(@TodayItem1);
			.@menu$ += ":Changed my mind";
			
			.@selected = select(.@menu$) - 1;
			if (.@selected == 2) {
				mes .npc_name$;
				mes "No rush, friend. Quality trades take time to consider.";
				close;
			}
			
			.@item_id = getd("@TodayItem" + .@selected);
			.@price = getd(".@price" + .@selected);
			
			mes .npc_name$;
			mes "Ah, the " + getitemname(.@item_id) + "! How many would you like to trade?";
			mes "I see you have " + countitem(.@item_id) + " with you.";
			next;
			
			input .@amount;
			if (.@amount <= 0) {
				mes .npc_name$;
				mes "Come now, let's be serious about our business dealings.";
				close;
			}
			if (countitem(.@item_id) < .@amount) {
				mes .npc_name$;
				mes "Hmm... you seem to be short on stock.";
				mes "You only have " + countitem(.@item_id) + " " + getitemname(.@item_id) + "(s).";
				mes "Shall we deal with what you have?";
				next;
				if(select("Yes, let's do it:No, maybe later") == 1) {
					.@amount = countitem(.@item_id);
				} else {
					mes .npc_name$;
					mes "Very well. Return when your inventory matches your ambition!";
					close;
				}
			}
			
			.@total = .@price * .@amount;
			if (#TRSH_DailySpend + .@total > .daily_spending_limit) {
				mes .npc_name$;
				mes "Ah, I'm afraid that exceeds my daily budget.";
				mes "I can only spend " + (.daily_spending_limit - #TRSH_DailySpend) + " zeny more today.";
				mes "Perhaps a smaller quantity?";
				close;
			}
			
			mes .npc_name$;
			mes "For " + .@amount + " " + getitemname(.@item_id) + "(s), I'll pay " + .@total + " zeny.";
			mes "A fair deal, wouldn't you say?";
			next;
			
			if(select("Deal!:I'll pass") == 2) {
				mes .npc_name$;
				mes "The door to opportunity remains open, friend.";
				close;
			}
			
			delitem .@item_id, .@amount;
			Zeny += .@total;
			#TRSH_DailySpend += .@total;
			#TRSH_TotalSpent += .@total;
			
			mes .npc_name$;
			mes "A pleasure doing business!";
			if (#TRSH_DailySpend >= .daily_spending_limit) {
				mes "And with that, I've reached my spending limit for today.";
				mes "But tomorrow brings new opportunities!";
			} else {
				mes "I'm always on the lookout for more treasures.";
				mes "Each day brings new interests!";
			}
			break;
			
		case 2:
			mes .npc_name$;
			mes "Curious about my ventures? I've invested " + #TRSH_TotalSpent + " zeny in treasures from adventurers like yourself.";
			mes "Every coin well spent, I'd say!";
			break;
			
		case 3:
			mes .npc_name$;
			mes "Safe travels, friend. Remember, today's refuse might be tomorrow's riches!";
			break;
	}
	close;
	
	/*-----------------------------------------------------
	Configuration
	-----------------------------------------------------*/
	OnInit:
		.npc_name$ = "[ ^0000FFGarrett the Scavenger^000000 ]";
		// Price limits configuration
		.increment = 10;     // Price multiplier
		.min_price = 100;    // Minimum price for any item
		.max_price = 2000;   // Maximum price for any item
		.daily_spending_limit = 1000000; // 1M zeny daily limit
		
		// Item list configuration - Common items and misc items
		setarray(.common_items[0],
			511, 609, 512, 967, 962, 960, 959, 955, 951, 948,
			943, 941, 938, 937, 936, 935, 934, 932, 930, 928,
			926, 924, 920, 919, 918, 916, 915, 913, 909, 908,
			907, 746);
			
		setarray(.misc_items[0],
			511, 609, 512, 967, 962, 960, 959, 955, 951, 948,
			943, 941, 938, 937, 936, 935, 934, 932, 930, 928,
			926, 924, 920, 919, 918, 916, 915, 913, 909, 908,
			907, 746,
			1099, 1098, 1096, 1095, 1057, 1056, 1055, 1054,
			1052, 1045, 1043, 1042, 1037, 1035, 1034, 1033,
			1028, 1024, 1023, 1022, 1020, 1019, 1018, 1001,
			7568, 7507, 7347, 7345, 7319, 7317, 7262, 7197,
			7188, 7167, 7150, 7126, 7123, 7122, 7116, 7070,
			7069, 7067, 7066, 7054, 7053, 7047, 7032, 7030,
			7006, 7005, 7004, 7001);
		end;
}
